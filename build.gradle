plugins {
    id 'java'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'io.github.ctrlaltdeletemenot'
version = '1.0.1'


repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

tasks {
    javadoc {
        options.encoding = "UTF-8"
    }
    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }
}

dependencies {
    testImplementation platform("org.junit:junit-bom:5.9.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    implementation "org.json:json:20231013"
}


test {
    systemProperty "integration.test.dir", project.file("temp")
    include("io/github/langneticus/core/**")
    include("io/github/langneticus/plugin/**")
    useJUnitPlatform()
}

gradlePlugin {
    website = "https://github.com/CtrlAltDeleteMeNot/io.github.langneticus"
    vcsUrl = "https://github.com/CtrlAltDeleteMeNot/io.github.langneticus.git"
    plugins.create("langneticusPlugin", new Action<PluginDeclaration>() {
        @Override
        void execute(PluginDeclaration pluginDeclaration) {
            pluginDeclaration.id = "io.github.ctrlaltdeletemenot.langneticus"
            pluginDeclaration.implementationClass = "io.github.langneticus.plugin.LangneticusPlugin"
            pluginDeclaration.displayName = "Langneticus plugin"
            pluginDeclaration.description = "Generate strong typed internationalization for your gradle java projects"
            pluginDeclaration.tags.set(["internationalization", "java", "strong-typed", "localization"])
        }
    })
}

wrapper {
    gradleVersion = '8.6'
}

def cleanIntegrationTests = tasks.register("cleanIntegrationTests") {
    doLast {
        def toClean = project.file("temp")
        if (toClean.exists() && toClean.isDirectory()) {
            toClean.deleteDir()
        }
    }
}

clean.dependsOn cleanIntegrationTests
test.dependsOn cleanIntegrationTests

